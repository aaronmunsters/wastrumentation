// This definition allows for implicit whitespaces, a new line makes no difference
WHITESPACE = _{ " " | "\r" | "\n" | "\t" }

// This definition allows for implicit comments
COMMENT = _{ "#|" ~ (!"|#" ~ ANY)* ~ "|#" }

// Input grammar
wasp_input = { SOI ~ wasp ~ EOI }
wasp       = { "(" ~ "aspect" ~ ("(" ~ advice_definition ~ ")")* ~ ")" }

// Guest language block, currently hardcoded AssemblyScript
GUEST_BLOCK = { ">>>GUEST>>>" ~ (!"<<<GUEST<<<" ~ ANY)* ~ "<<<GUEST<<<" }

// Advice definitions
advice_definition = { advice_global | advice_trap }
advice_global     = { "global" ~ GUEST_BLOCK }
advice_trap       = { "advice" ~ trap_signature }

// The definition of identifiers
identifier      = @{ ASCII_ALPHA+ }
type_identifier = @{ ASCII_ALPHA_UPPER ~ ASCII_ALPHANUMERIC+ }

// Advice traps
trap_signature = {
    trap_apply
  | trap_call
  | trap_block_pre
  | trap_block_post
  | trap_loop_pre
  | trap_loop_post
  | trap_select
  | trap_call_indirect_pre
  | trap_call_indirect_post
  | trap_if_then
  | trap_if_then_else
  | trap_br_if
  | trap_br_table /* | more | trap | targets | here | ... */
}

// TRAP APPLY
trap_apply           = { "apply" ~ apply_hook_signature ~ GUEST_BLOCK }
apply_hook_signature = { apply_gen | apply_spe_intro | apply_spe_inter }

// [ ðŸŒ / (ðŸ‡) ], [ ðŸ” / ðŸ“ ], [ ðŸ™†â€ ]
apply_gen = { apply_formal_wasm_f ~ apply_formal_argument ~ apply_formal_result }
// [ ðŸ‡ ], [ ðŸ” ], [ ðŸª– ]
apply_spe_intro = { apply_formal_wasm_f ~ "(" ~ apply_formal_argument+ ~ ")" ~ "(" ~ apply_formal_result+ ~ ")" }
// [ ðŸ‡ ], [ ðŸ“ ], [ ðŸª– ]
apply_spe_inter = { apply_formal_wasm_f ~ "(" ~ "Mut" ~ apply_formal_argument+ ~ ")" ~ "(" ~ "Mut" ~ apply_formal_result+ ~ ")" }

apply_formal_wasm_f   = { "(" ~ identifier ~ "WasmFunction" ~ ")" }
apply_formal_argument = { typed_argument }
apply_formal_result   = { typed_argument }
typed_argument        = { "(" ~ identifier ~ type_identifier ~ ")" }

branch_formal_condition = { "(" ~ identifier ~ "Condition" ~ ")" }
branch_formal_label     = { "(" ~ identifier ~ "Label" ~ ")" }

call_qualifier = { "pre" | "post" }

// TRAP CALL
trap_call     = { "call" ~ call_qualifier ~ formal_target ~ GUEST_BLOCK }
formal_target = { "(" ~ identifier ~ "FunctionIndex" ~ ")" }

// TRAP BLOCK
trap_block_pre  = { "block" ~ "pre" ~ GUEST_BLOCK }
trap_block_post = { "block" ~ "post" ~ GUEST_BLOCK }

// TRAP LOOP
trap_loop_pre  = { "loop" ~ "pre" ~ GUEST_BLOCK }
trap_loop_post = { "loop" ~ "post" ~ GUEST_BLOCK }

// TRAP SELECT
trap_select             = { "select" ~ select_formal_condition ~ GUEST_BLOCK }
select_formal_condition = { "(" ~ identifier ~ "Condition" ~ ")" }

// TRAP CALL_INDIRECT
trap_call_indirect_pre  = { "call_indirect" ~ "pre" ~ formal_table ~ formal_index ~ GUEST_BLOCK }
trap_call_indirect_post = { "call_indirect" ~ "post" ~ formal_table ~ GUEST_BLOCK }
formal_table            = { "(" ~ identifier ~ "FunctionTable" ~ ")" }
formal_index            = { "(" ~ identifier ~ "FunctionTableIndex" ~ ")" }

// TRAP IF - [ ðŸ‡ ], [ ðŸ“ ], [ ðŸª– ]
trap_if_then      = { "if_then" ~ branch_formal_condition ~ GUEST_BLOCK }
trap_if_then_else = { "if_then_else" ~ branch_formal_condition ~ GUEST_BLOCK }

// TRAP IF-BR - [ ðŸ‡ ], [ ðŸ“ ], [ ðŸª– ]
trap_br_if = { "br_if" ~ branch_formal_condition ~ branch_formal_label ~ GUEST_BLOCK }

// TRAP BR-TABLE - [ ðŸ‡ ], [ ðŸ“ ], [ ðŸª– ]
trap_br_table         = { "br_table" ~ branch_formal_target ~ branch_formal_default ~ GUEST_BLOCK }
branch_formal_target  = { "(" ~ identifier ~ "Target" ~ ")" }
branch_formal_default = { "(" ~ identifier ~ "Default" ~ ")" }
